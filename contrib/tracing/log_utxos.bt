#!/usr/bin/env bpftrace

/*

  USAGE:

  bpftrace contrib/tracing/log_utxos.bt

  This script requires a 'bitcoind' binary compiled with eBPF support and the
  'utxocache' tracepoints. By default, it's assumed that 'bitcoind' is
  located in './src/bitcoind'. This can be modified in the script below.

  NOTE: requires bpftrace v0.12.0 or above.
*/

BEGIN
{
    printf("%-7s %-71s %16s %7s %8s\n",
          "OP", "Outpoint", "Value", "Height", "Coinbase");
}

/*
  Attaches to the 'utxocache:add' tracepoint and prints additions to the UTXO set cache.
*/
usdt:./src/bitcoind:utxocache:add
{
  $txid = (uint8*)arg0;
  $index = (uint32)arg1;
  $height = (uint32)arg2;
  $value = (int64)arg3;
  $isCoinbase = arg4;

  printf("Added   ");
  $i = 32;
  while ($i != 0) {
    $i--;
    printf("%02x", *($txid + $i));
  }

  printf(":%-6d %16ld %7d %s\n", $index, $value, $height, ($isCoinbase ? "Yes" : "No" ));
}

/*
  Attaches to the 'utxocache:spent' tracepoint and prints spents from the UTXO set cache.
*/
usdt:./src/bitcoind:utxocache:spent
{
  $txid = (uint8*)arg0;
  $index = (uint32)arg1;
  $height = (uint32)arg2;
  $value = (int64)arg3;
  $isCoinbase = arg4;

  printf("Spent   ");
  $i = 32;
  while ($i != 0) {
    $i--;
    printf("%02x", *($txid + $i));
  }

  printf(":%-6d %16ld %7d %s\n", $index, $value, $height, ($isCoinbase ? "Yes" : "No" ));
}

/*
  Attaches to the 'utxocache:uncache' tracepoint and uncache UTXOs from the UTXO set cache.
*/
usdt:./src/bitcoind:utxocache:uncache
{
  $txid = (uint8*)arg0;
  $index = (uint32)arg1;
  $height = (uint32)arg2;
  $value = (int64)arg3;
  $isCoinbase = arg4;

  printf("Uncache ");
  $i = 32;
  while ($i != 0) {
    $i--;
    printf("%02x", *($txid + $i));
  }

  printf(":%-6d %16ld %7d %s\n", $index, $value, $height, ($isCoinbase ? "Yes" : "No" ));
}
